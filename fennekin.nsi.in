#
# Change these values to fit your application...
#

!define APPNAME "fennekin" # Full app name, like: Gtk+ 2.0 Hello World
!define PROGNAME "@PACKAGE@" # executable name, like: gtk2hello
!define PROG_VERSION "@PACKAGE_VERSION@" # the program version, like: 0.3.0
!define ICON_NAME "simba64x64.ico" # filename of icon to use for this app, this file should be in src/
!define COMPANYNAME "Bert van der Weerd" # Your name, or company
!define ESTIMATED_SIZE 16000 # Estimated size (in KB) of installed program for use in "add or remove programs"

#
# Change these values to fit your mingw and gtk2 installation...
#

!define MINGW_BINDIR "C:\MinGW\bin"
!define GTK2_BINDIR "C:\MinGW\gtk2\bin"

#
# The actual installer/uninstaller, you should not need to change much here below
#

Name "${PROGNAME}"
OutFile "${PROGNAME}-${PROG_VERSION}-win32-setup.exe"
InstallDir $PROGRAMFILES\${PROGNAME}
RequestExecutionLevel admin

Page directory
Page instfiles

function .onInit
	setShellVarContext all
functionEnd
 
Section "${PROGNAME}"

	# Copy files
	SetOutPath $INSTDIR
	File /r INSTDIR\*.*
	SetOutPath $INSTDIR\bin
	File src\${ICON_NAME}
	File /r ${GTK2_BINDIR}\*.dll
	File ${MINGW_BINDIR}\libgcc_s_dw2-1.dll
	File ${MINGW_BINDIR}\libstdc++-6.dll

	# Start Menu
	createDirectory "$SMPROGRAMS\${COMPANYNAME}"
	createShortCut "$SMPROGRAMS\${COMPANYNAME}\${APPNAME}.lnk" "$INSTDIR\bin\${PROGNAME}.exe" "" "$INSTDIR\bin\${ICON_NAME}"

	# Uninstaller 
	writeUninstaller "$INSTDIR\bin\uninstall.exe"

	# Registry information for add/remove programs
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}" "DisplayName" "${APPNAME}"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}" "UninstallString" "$\"$INSTDIR\bin\uninstall.exe$\""
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}" "QuietUninstallString" "$\"$INSTDIR\bin\uninstall.exe$\" /S"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}" "InstallLocation" "$\"$INSTDIR$\""
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}" "DisplayIcon" "$\"$INSTDIR\bin\${ICON_NAME}$\""
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}" "Publisher" "${COMPANYNAME}"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}" "DisplayVersion" "${PROG_VERSION}"
	# There is no option for modifying or repairing the install
	WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}" "NoModify" 1
	WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}" "NoRepair" 1
	# Set the INSTALLSIZE constant (!defined at the top of this script) so Add/Remove Programs can accurately report the size
	WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}" "EstimatedSize" ${ESTIMATED_SIZE}

SectionEnd

# Before uninstall, ask for confirmation
function un.onInit
	SetShellVarContext all
 
	#Verify the uninstaller - last chance to back out
	MessageBox MB_OKCANCEL "Permanantly remove program: ${APPNAME}?" IDOK next
		Abort
	next:
functionEnd

# Uninstaller
section "uninstall"

	# Remove Start Menu launcher
	delete "$SMPROGRAMS\${COMPANYNAME}\${APPNAME}.lnk"
	# Try to remove the Start Menu folder - this will only happen if it is empty
	rmDir "$SMPROGRAMS\${COMPANYNAME}"
 
	# Remove files
	rmDir /r $INSTDIR

	# Remove uninstaller information from the registry
	DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${COMPANYNAME} ${APPNAME}"

sectionEnd
